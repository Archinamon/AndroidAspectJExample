buildscript {
    ext.kotlin_version = '1.0.4'

    repositories {
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath 'com.github.Archinamon:GradleAspectJ-Android:2.2.2'
        classpath 'me.tatarka:gradle-retrolambda:3.3.1'
        //APT
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
        //Kotlin
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'groovyx.grooid.groovy-android'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.archinamon.aspectj'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

aspectj {
    debugInfo !isTCBuild()
}

retrolambda {
    defaultMethods true
}

androidGroovy {
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'
}

apt {
    arguments {
        resourcePackageName android.defaultConfig.applicationId
        androidManifestFile variant.outputs[0].processResources.manifestFile
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion '23.0.3'

    defaultConfig {
        applicationId "com.archinamon.example"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

repositories {
    mavenCentral()
}

def AAVersion = '3.3' as String
dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    apt "org.androidannotations:androidannotations:$AAVersion"
    compile "org.androidannotations:androidannotations-api:$AAVersion"

    compile 'org.codehaus.groovy:groovy:2.4.5:grooid'

    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'org.jetbrains.kotlin:kotlin-stdlib:1.0.1'
    compile 'com.google.dagger:dagger:2.0'
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.squareup.picasso:picasso:2.1.1'
    compile 'com.android.support:design:25.0.0'
    testCompile 'junit:junit:4.12'
    apt 'com.google.dagger:dagger-compiler:2.0'
    provided 'org.glassfish:javax.annotation:10.0-b28'
}

def isTCBuild() {
    return project.properties.containsKey('teamcity');
}
